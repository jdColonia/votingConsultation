//
// Install the gradle Ice Builder plug-in from the plug-in portal
//
plugins {
    id 'com.zeroc.gradle.ice-builder.slice' version '1.5.0' apply false
    id 'com.github.johnrengelman.shadow' version '7.1.1' apply false
}

subprojects {
    //
    // Apply Java and Ice Builder plug-ins to all sub-projects
    //
    apply plugin: 'java'
    apply plugin: 'com.zeroc.gradle.ice-builder.slice'
    apply plugin: 'com.github.johnrengelman.shadow'

    //
    // Both Client and Server projects share the Printer.ice Slice definitions
    //
    slice {
        java {
            files = [file("../VotingConsultation.ice")]
        }
    }

    //
    // Use Ice JAR files from maven central repository
    //
    repositories {
        mavenCentral()
    }

    //
    // Both Client and Server depend only on Ice JAR
    //
    dependencies {
        implementation 'com.zeroc:ice:3.7.10'
        implementation 'com.zeroc:icegrid:3.7.10'
        implementation 'org.apache.poi:poi:5.0.0'
        implementation 'org.apache.poi:poi-ooxml:5.0.0'
        implementation 'org.postgresql:postgresql:42.2.24'
        implementation 'io.github.cdimascio:java-dotenv:5.2.2'
    }

    //
    // Create a JAR file with the appropriate Main-Class and Class-Path attributes
    //
    jar {
        manifest {
            attributes(
                    "Main-Class": project.name.capitalize(),
                    // "Class-Path": '/opt/Ice-3.7.6/lib/ice-3.7.6.jar'
                    "Class-Path": configurations.runtimeClasspath.resolve().collect { it.toURI() }.join(' ')
            )
        }
    }
    shadowJar {
        archiveBaseName.set(project.name)
        archiveClassifier.set('')
        archiveVersion.set('')
        mergeServiceFiles()
    }
}